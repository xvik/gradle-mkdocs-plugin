plugins {
    id 'com.gradle.plugin-publish' version '1.3.1'
    id 'java-gradle-plugin'
    id 'groovy'
    id 'jacoco'
    id 'signing'
    id 'net.researchgate.release' version '3.1.0'
    id 'ru.vyarus.quality' version '5.0.0'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
    id 'ru.vyarus.java-lib' version '3.0.0'
    id 'ru.vyarus.github-info' version '2.0.0'
    id 'com.github.ben-manes.versions' version '0.52.0'
    id "pl.droidsonroids.jacoco.testkit" version "1.0.12"
    id 'ru.vyarus.mkdocs' version '4.0.1'
}

java {
    sourceCompatibility = 1.8
}

wrapper {
    gradleVersion = '8.6'
    distributionType = Wrapper.DistributionType.BIN
}

ext.pythonPlugin = '4.1.0'

repositories { mavenLocal(); mavenCentral(); gradlePluginPortal() }
dependencies {
    implementation "ru.vyarus:gradle-use-python-plugin:$pythonPlugin"

    // jgit 5.x is still supported so no need to update to 6.x yet
    // Also, no need to update to grgit 5.x as there are no important updates
    implementation 'org.ajoberstar.grgit:grgit-core:4.1.1'
    implementation 'org.eclipse.jgit:org.eclipse.jgit:5.13.3.202401111512-r'

    testImplementation('org.spockframework:spock-core:2.3-groovy-3.0') {
        exclude group: 'org.codehaus.groovy'
    }
}

group = 'ru.vyarus'
description = 'Mkdocs documentation plugin'

github {
    user 'xvik'
    license 'MIT'
}

mkdocs {
    extras = [
            'version': '4.0.1',
            'pythonPlugin': pythonPlugin
    ]
    publish {
        docPath = mkdocs.extras['version']
        rootRedirect = true
        rootRedirectTo = 'latest'
        versionAliases = ['latest']
        hideOldBugfixVersions = true
    }
}
afterEvaluate {
    python.modules.each {
        def mod = it.split(':')
        mkdocs.extras[mod[0]] = mod[1]
    }
}

maven.pom {
    developers {
        developer {
            id = 'xvik'
            name = 'Vyacheslav Rusakov'
            email = 'vyarus@gmail.com'
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username = findProperty('sonatypeUser')
            password = findProperty('sonatypePassword')
        }
    }
}

// skip signing for jitpack (snapshots)
tasks.withType(Sign) {onlyIf { !System.getenv('JITPACK') }}

// Required signing properties for release: signing.keyId, signing.password and signing.secretKeyRingFile
// (https://docs.gradle.org/current/userguide/signing_plugin.html#sec:signatory_credentials)

javaLib {
    // don't publish gradle metadata artifact
    withoutGradleMetadata()
}


gradlePlugin {
    plugins {
        mkdocsPlugin {
            id = 'ru.vyarus.mkdocs'
            displayName = project.description
            description = 'Mkdocs documentation generation and publishing plugin'
            tags.set(['documentation', 'mkdocs'])
            implementationClass = 'ru.vyarus.gradle.plugin.mkdocs.MkdocsPlugin'
        }
        mkdocsBuildPlugin {
            id = 'ru.vyarus.mkdocs-build'
            displayName = "${project.description} (without publication)"
            description = 'Mkdocs documentation generation plugin'
            tags.set(['documentation', 'mkdocs'])
            implementationClass = 'ru.vyarus.gradle.plugin.mkdocs.MkdocsBuildPlugin'
        }
    }
}

release.git.requireBranch.set('master')

afterReleaseBuild {
    dependsOn = [
            'publishMavenPublicationToSonatypeRepository',
            'closeAndReleaseSonatypeStagingRepository',
            publishPlugins]
    doLast {
        logger.warn "RELEASED $project.group:$project.name:$project.version"
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events 'skipped', 'failed'
        exceptionFormat 'full'
    }
    maxHeapSize = '512m'
    doLast {
        sleep(1000)
    }
}

dependencyUpdates.revision = 'release'
